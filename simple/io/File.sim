/* 
	Copyright (c) 2017-2018 Azeez Adewale <azeezadewale98@gmail.com"> 
	MIT License Copyright (c) 2018 simple 

*/

/* 
 * #filename - File.sim
 * #author - Azeez Adewale
 * #date - 5 Febuary 2017
 */
 
module simple.io

call "simple/core/String.sim"
call "Path.sim"

FILE_DOES_NOT_EXIST = "The file does not exist"

class File 	

	block File(path)
		if isPath(path)
			filePath = path.toString()
		else 
			filePath = path
		end
		filePath = replaceString(filePath, "\\", "/")
		
	block absolutePath()
		return filePath
		
	block name()
		return lastStringAfterChar(filePath, "/")
		
	block directory()
		return removeString(filePath, "/"+name())
		
	block toPath()
		return new Path(filePath)
		
	block readFileAsString()
		ensureExistence()
		return readfile(filePath)
		
	block write(content)
		ensureExistence()
		writefile(filePath, content)
		
	block append(content)
		ensureExistence()
		writefile(filePath, readFileAsString()+""+content)
		
	block appendBoolean(content)
		ensureExistence()
		if content == true 
			content = "true"
		else 
			content = "false"
		end
		writefile(filePath, readFileAsString()+""+content)
		
	block exists() 
		return __exists(filePath)
	
	private
		filePath = "(null)"
		
		block ensureExistence()
			if !exists() 
				throw(FILE_DOES_NOT_EXIST+" : "+name())
			end
		
	